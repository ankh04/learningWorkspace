## 编程语言的流行度
可以参考[Tiobe](w.tiobe.com/tiobe-index/)

## 各类编程语言的编译器使用的语言

| 语言                                                                   | 编译器使用的语言             |
| ---------------------------------------------------------------------- | ---------------------------- |
| C                                                                      | gcc(C,Ada,C++,Go)            |
| [Go](https://github.com/golang/go)                                     | Go(90%),ASM(5%)              |
| [Python](https://github.com/python/cpython)                            | Python(65%), C(32%)          |
| [Ruby](https://github.com/ruby/ruby)                                   | Ruby(64%),C(28%, yacc)       |
| [Io](https://github.com/IoLanguage/iohttps://github.com/IoLanguage/io) | C(63%),Io(28%)               |
| [Java](https://github.com/openjdk/jdk)                                 | Java(76%), C(13%), ASM(2.4%) |
| [JavaScript](https://github.com/v8/v8)                                 | C++(68%),JS(29%)             |


## Io语言
- Io的语法简洁, 语法糖很少
- Io中万物皆消息
- Io很适合用来写[[DSL|DSL(特定领域语言)]]

对于`emmet`, 可以轻松的用Io语言实现:
```Io
forward := method(
    call message name
)

root := nil

Html := Object clone do(
    OperatorTable addOperator("@", 15)
    OperatorTable addOperator("$", 15)
    OperatorTable addOperator(">", 15)
    OperatorTable addOperator("+", 15)
    OperatorTable addOperator("*", 15)
    OperatorTable addOperator("!", 15)

    @ := method(classname,
        attrs append(" class=\"#{classname}\"" interpolate)
        self
    )

    $ := method(id,
        attrs append(" id=\"#{id}\"" interpolate)
        self
    )

    > := method(tag,
        html := Html clone
        html tag := tag
        html parent := self
        self childs append(html)
        html
    )

    + := method(tag,
        html := nil
        if(self parent,
            html = Html clone
            html tag := tag
            self parent childs append(html)
        )
        html
    )

    * := method(count,
        self count := count asNumber
        self
    )

    ! := method(content,
        self content := content
        self
    )

    forward := method(
        name := call message name
        html := Html clone
        html tag := name
        root = html
        html
    )

    init := method(
        self childs := list()
        self attrs := list()
        self count := 1
        self parent := nil
        self content := nil
    )
)

Html flush := method(
    root render("")
)

Html render := method(indent,
    self count repeat(
        writeln(indent,"<",self tag,self attrs join(""),">")
        childIndent := indent .. "  "
        if(self content,
            writeln(childIndent,self content)
        )
        self childs foreach(index,arg,
            arg render(childIndent)
        )
        writeln(indent,"</",self tag,">")
    )
)


zencode := method(html,
    html flush
)

doFile("enhance_html.io")
```


